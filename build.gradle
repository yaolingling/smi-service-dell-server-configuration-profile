apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.sonarqube'
apply plugin: 'nebula.rpm'
apply plugin: 'nebula.deb'
apply plugin: "com.github.hierynomus.license"
//apply plugin: 'nebula.lint'

// rpm/deb package variables
def _vendor = 'Dell'
def _license = 'Apache License 2.0'
def _group = ''
def _url = 'http://support.dell.com'
def _arch = 'x86_64'
def _os = 'LINUX'
def _release = 1
def _sourcePaths=[]

sourceCompatibility = 1.8
version = "${version}"
dockerTag = "${dockerTag}"
group = 'com.dell.isg.smi'

buildscript {
	ext {
		springVersion = '4.3.6.RELEASE'
    	springBootVersion = '1.5.1.RELEASE'
    	springCloudVersion = 'Camden.SR5'
	}
   repositories {
    mavenLocal()
    mavenCentral()
	maven {
		url "${artifactory_contextUrl}/libs-release"
		url "https://plugins.gradle.org/m2/"

	}
  }
  dependencies {
  	classpath "io.spring.gradle:dependency-management-plugin:+"
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	classpath(group: 'com.netflix.nebula', name: 'gradle-ospackage-plugin', version: '+' )
	classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:+"
	classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
	//classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
  }
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

//gradleLint {
//  rules  = ['dependency-parentheses','overridden-dependency-version','duplicate-dependency-class', 'all-dependency','unused-dependency']
//}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Dell server configuration profile jar',
                   'Implementation-Version': version
    }
	baseName = 'dell-server-configuration-profile'
}

springBoot {
    executable = true
}

repositories {
	mavenLocal()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
	maven { url "https://oss.sonatype.org/content/repositories/releases" }
	maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {
	compile "com.dell.isg.smi:wsmanlib:1.0.61"
	compile "com.dell.isg.smi:wsmanclient:1.0.37"
    compile 'com.dell.isg.smi:commons-elm:1.0.76'
    compile 'com.dell.isg.smi:commons-utilities:1.0.32'
    compile 'com.dell.isg.smi:commons-model:1.0.89'
    compile 'com.dell.isg.smi:adapter-server:1.0.71'
	compile "org.springframework.boot:spring-boot-starter-web"
	compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile 'org.springframework.cloud:spring-cloud-starter-consul-all'
	compile "org.springframework:spring-web"
	runtime group: 'com.dell.isg.smi', name: 'wiseman', version: '1.0.5'
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
	compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'
	compile group: "io.springfox", name: "springfox-swagger2", version: "2.7.0"
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'
	compile group: 'org.eclipse.persistence', name: 'org.eclipse.persistence.moxy', version: '2.7.0-RC1'
	compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'
}

test {
    systemProperties 'property': 'value'
}

sonarqube {
    properties {
        property "sonar.projectName", "SMI :: Service :: Server configuration profile"
        property "sonar.projectKey", "org.sonarqube:service-server-configuration-profile"
    }
}

license {
        header rootProject.file('license-template')
        includes(["**/*.java", "**/*.properties"])
        strictCheck true
        ignoreFailures true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    	ext.name = 'DELL Inc.'
}

task generateRpm(type: Rpm){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Dell server-configuration-profile service'
	description = 'Installs and runs the server-configuration-profile microservice as a linux service'
	url = _url

	packageName = 'dell-server-configuration-profile'
	release = _release
	arch = _arch
	os = _os

	from fileTree( "build/libs" ), {
        include "*.jar"
        fileMode = 0755
		user = "root"
		addParentDirs = false
	    into  '/opt/dell/server-configuration-profile'
    }

	from fileTree("./"), {
        include "application.yml"
        fileMode = 0755
		//user = "root"
		//addParentDirs = true
	    into  '/opt/dell/server-configuration-profile'
    }


	from fileTree( "pkg" ), {
        include "dell-server-configuration-profile.service"
		fileMode = 0777
	    into  '/etc/systemd/system'
    }
	link('/etc/systemd/system/multi-user.target.wants/dell-server-configuration-profile.service', '/etc/systemd/system/dell-server-configuration-profile.service')
}

task generateDeb(type: Deb){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Dell server-configuration-profile service'
	description = 'Installs and runs the server-configuration-profile microservice as a linux service'
	url = _url

	requires( 'postgresql', '9.4', GREATER | EQUAL)

	packageName = 'dell-server-configuration-profile'
	release = _release
	arch = 'all'
	os = _os

	from fileTree( "build/libs" ), {
        include "*.jar"
        fileMode = 0755
		user = "root"
		addParentDirs = true
	    into  '/opt/dell/server-configuration-profile'
    }

	from fileTree("./"), {
        include "application.yml"
        fileMode = 0755
		//user = "root"
		//addParentDirs = true
	    into  '/opt/dell/server-configuration-profile'
    }

	from fileTree( "pkg" ), {
        include "dell-server-configuration-profile.service"
		fileMode = 0777
	    into  '/etc/systemd/system'
    }
	link('/etc/systemd/system/multi-user.target.wants/dell-server-configuration-profile.service', '/etc/systemd/system/dell-server-configuration-profile.service')
}


task buildSourcePathsArray(dependsOn: 'eclipseClasspath' ){
	doLast{
		def classpathFile = file('.classpath')
		if( classpathFile.exists() ) {
			def cpXml = new XmlParser().parse(classpathFile)
			cpXml.classpathentry.each {
				if('lib' == it.@kind ) {
					if( it.@sourcepath == null ){
						println 'missing source jar for:' + it.@path
					}
					else{
					 	_sourcePaths.add(it.@sourcepath)
					}
				}
			}
		}
		 
		def arrayLength = _sourcePaths.size()
		println 'sourcePaths length is:' + arrayLength
	}
}


task dependencySourcesZip(type: Zip, dependsOn: 'buildSourcePathsArray' ) {
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()
	baseName = "smi-service-dell-server-configuration-profile-dependency-sources"
	version = "${dockerTag}"
	outputs.upToDateWhen { false }
 
    from _sourcePaths
}
